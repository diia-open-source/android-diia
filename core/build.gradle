plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
}

apply from: '../dependencies.gradle'

android {
    namespace 'ua.gov.diia.core'
    compileSdk 34

    defaultConfig {
        minSdk 23
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    flavorDimensions = ["vendor"]
    productFlavors {
        gplay {
            dimension "vendor"
        }

        huawei {
            dimension "vendor"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        prod {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
        stage {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
        vendors {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }

    // work-runtime-ktx 2.1.0 and above now requires Java 8
    kotlinOptions {
        jvmTarget = "11"
    }

    kapt {
        correctErrorTypes true
    }

    lint {
        disable 'MissingTranslation'
    }

    buildFeatures {
        compose = true
    }
}

dependencies {
    implementation deps.activity_ktx
    implementation deps.fragment_ktx
    implementation deps.legacy_support
    implementation deps.appcompat
    //lifecycle
    implementation deps.lifecycle_extensions
    implementation deps.lifecycle_livedata_ktx
    implementation deps.lifecycle_viewmodel_ktx
    //navigation
    implementation deps.navigation_fragment_ktx
    implementation deps.navigation_ui_ktx
    //retrofit
    implementation deps.retrofit
    // Moshi
    implementation deps.moshi
    implementation deps.moshi_adapters
    implementation deps.moshi_kotlin
    implementation deps.retrofit_moshi_converter
    kapt deps.moshi_codegen
    //Desugaring
    coreLibraryDesugaring deps.desugar_jdk_libs
    //hilt
    implementation deps.hilt_android
    kapt deps.hilt_android_compiler
    implementation deps.hilt_navigation_fragment
    implementation deps.hilt_work
    kapt deps.hilt_compiler
    //viewpager
    implementation deps.viewpager
    //glide
    implementation deps.glide
    kapt deps.glide_compiler
    //lottie
    implementation deps.lottie

    implementation deps.better_link_movement_method

    //work
    implementation deps.work_runtime_ktx

    //Compose
    implementation deps.activity_compose

    //Video
    implementation deps.light_compressor

    //test
    testImplementation deps.junit
    testImplementation deps.mockk_android
    testImplementation deps.mockk_agent
    testImplementation deps.turbine
    testImplementation deps.kotlinx_coroutines_test
    testImplementation deps.androidx_core_testing
    testImplementation deps.hamcrest_library
    testImplementation deps.json
    androidTestImplementation deps.android_junit
    androidTestImplementation deps.android_espresso_core
}

tasks.withType(Test) {
    // https://github.com/mockk/mockk/issues/681
    jvmArgs("--add-opens", "java.base/java.time=ALL-UNNAMED")
}

apply from: '../jacoco.gradle'